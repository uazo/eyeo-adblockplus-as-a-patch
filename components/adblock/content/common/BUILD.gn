#
# This file is part of eyeo Chromium SDK,
# Copyright (C) 2006-present eyeo GmbH
#
# eyeo Chromium SDK is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License version 3 as
# published by the Free Software Foundation.
#
# eyeo Chromium SDK is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with eyeo Chromium SDK.  If not, see <http://www.gnu.org/licenses/>.

import("//components/adblock/features.gni")

config("adblock_content_common_config") {
  defines = []

  if (eyeo_intercept_debug_url) {
    print("WARNING! Enabled intercepting eyeo debug url \"adblock.test.data\"")
    defines += [ "EYEO_INTERCEPT_DEBUG_URL=1" ]
  }
}

source_set("common_impl") {
  visibility = [
    ":*",
    "//components/adblock/content:*",
  ]
  sources = [
    "adblock_url_loader_factory.cc",
    "adblock_url_loader_factory.h",
  ]

  if (eyeo_intercept_debug_url) {
    sources += [
      "adblock_url_loader_factory_for_test.cc",
      "adblock_url_loader_factory_for_test.h",
    ]
  }

  deps = [
    "//components/adblock/core",
    "//third_party/blink/public/common:headers",
  ]

  public_deps =
      [ "//components/adblock/content/common/mojom:adblock_interfaces" ]

  all_dependent_configs = [ ":adblock_content_common_config" ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "test/mock_adblock_mojo_interface.cc",
    "test/mock_adblock_mojo_interface.h",
  ]

  deps = [ "//components/adblock/content/common/mojom:adblock_interfaces" ]

  public_deps = [
    "//testing/gmock",
    "//testing/gtest",
  ]
}
